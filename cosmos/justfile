# Command runner for Mercator backend
# This is a [justfile](https://github.com/casey/juste)

# set the default shell (to be safe)
set shell := ["zsh", "-uc"]

git_commit_hash := `git rev-parse HEAD`

default:
	@just --list

test:
	poetry run pytest --cov=app test

run:
	poetry run ./start.sh

lint:
	poetry run mypy .
	poetry run pylint app test

format:
	poetry run isort --quiet .
	poetry run black .

db-local db input_file *args:
	createdb {{ db }}
	psql {{ db }} -c "CREATE EXTENSION postgis;"
	osm2pgsql -d {{ db }} -O flex -S ./osm-docker/style.lua {{ args }} {{ input_file }}
	psql {{ db }} -f ./osm-docker/docker-entrypoint-initdb.d/30_post-import.sql
	psql {{ db }} -f ./osm-docker/docker-entrypoint-initdb.d/41_cardinal_direction.sql
	psql {{ db }} -f ./osm-docker/docker-entrypoint-initdb.d/42_create_function_to_geojson_feature.sql
	psql {{ db }} -f ./osm-docker/docker-entrypoint-initdb.d/43_create_function_to_geojson_feature_collection.sql

create-openapi-schema:
	poetry run python -m app.cli.create_openapi --output openapi.json